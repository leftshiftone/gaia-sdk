import org.gradle.api.tasks.bundling.Jar

/*
 * Copyright (c) 2016-2019, Leftshift One
 * __________________
 * [2019] Leftshift One
 * All Rights Reserved.
 * NOTICE:  All information contained herein is, and remains
 * the property of Leftshift One and its suppliers,
 * if any.  The intellectual and technical concepts contained
 * herein are proprietary to Leftshift One
 * and its suppliers and may be covered by Patents,
 * patents in process, and are protected by trade secret or copyright law.
 * Dissemination of this information or reproduction of this material
 * is strictly forbidden unless prior written permission is obtained
 * from Leftshift One.
 */

allprojects {
    apply plugin: "kotlin"
    apply plugin: "signing"

    repositories {
        jcenter()
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation "org.jetbrains.kotlin:kotlin-reflect"
        implementation "org.slf4j:slf4j-api:1.7.28"
        implementation "org.reactivestreams:reactive-streams:1.0.3"
        implementation "com.fasterxml.jackson.core:jackson-databind:2.10.0"
        implementation "io.projectreactor.netty:reactor-netty:1.0.0"
        implementation "io.projectreactor:reactor-core:3.4.0"
        implementation "com.hivemq:hivemq-mqtt-client:1.2.0"

        testImplementation "org.junit.jupiter:junit-jupiter"
        testImplementation "org.assertj:assertj-core:3.11.1"

        testRuntime "org.slf4j:slf4j-simple:1.7.28"
    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    test {
        useJUnitPlatform()
    }

    tasks.withType(Test) { testTask ->
        maxHeapSize = "1G"
        testTask.beforeTest { descriptor ->
            logger.lifecycle(descriptor.toString())
        }
    }
}

subprojects {
    apply plugin: "org.jetbrains.dokka"
    apply plugin: "one.leftshift.asteria-publish"
    apply plugin: "maven-publish"

    task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
        outputFormat = "javadoc"
        outputDirectory = "${buildDir}/dokkaJavadoc"
    }
    task javadocJar(type: Jar) {
        from dokkaJavadoc
        classifier "javadoc"
    }
    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier "sources"
    }
    artifacts {
        archives javadocJar, sourcesJar
    }
    asteriaPublish {
        enableBranchSnapshotRepositories = true
    }
    publishing {
        publications {
            release(MavenPublication) {
                artifactId = project.name
                from components.java
                artifact sourceJar
                artifact javadocJar
                pom.withXml {
                    Node root = asNode()
                    root.appendNode("name", "GAIA SDK")
                    root.appendNode("description", "GAIA SDK is an open source software library in order to interact with the GAIA API.")
                    root.appendNode("url", "https://github.com/leftshiftone/gaia-sdk")

                    Node licenses = root.appendNode("licenses")
                    Node license = licenses.appendNode("license")
                    license.appendNode("name", "MIT")
                    license.appendNode("url", "https://github.com/leftshiftone/gaia-sdk/blob/master/LICENSE")

                    Node developers = root.appendNode("developers")
                    Node developer = developers.appendNode("developer")
                    developer.appendNode("id", "leftshiftone")
                    developer.appendNode("name", "Leftshift One")
                    developer.appendNode("email", "contact@leftshift.one")
                    developer.appendNode("organization", "Leftshift One Software GmbH")
                    developer.appendNode("organizationUrl", "https://www.leftshift.one")

                    Node scm = root.appendNode("scm")
                    scm.appendNode("connection", "scm:git:git://github.com/leftshiftone/gaia-sdk.git")
                    scm.appendNode("developerConnection", "scm:git:ssh://github.com:leftshiftone/gaia-sdk.git")
                    scm.appendNode("url", "https://github.com/leftshiftone/gaia-sdk")
                }
            }
        }
        repositories {
            maven {
                name = "sonatype"
                url = version.toString().endsWith("SNAPSHOT") ? "https://s01.oss.sonatype.org/content/repositories/snapshots" : "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username System.getenv("OSSRH_LOGIN_NAME")
                    password System.getenv("OSSRH_LOGIN_PASSWORD")
                }
            }
        }
        ext."signing.keyId" = System.getenv("OSSRH_SIGN_KEY_ID")
        ext."signing.password" = System.getenv("OSSRH_SIGN_KEY_PASSWORD")
        ext."signing.secretKeyRingFile" = System.getenv("OSSRH_SIGN_KEY_PATH")
        signing {
            sign publishing.publications
        }
    }

    if (System.getenv("CI") == null || System.getenv("CI") != "true") {
        rootProject.tasks.release.dependsOn tasks.generateLock
        rootProject.tasks.release.dependsOn tasks.saveLock
    }
}
ext."signing.keyId" = System.getenv("OSSRH_SIGN_KEY_ID")
ext."signing.password" = System.getenv("OSSRH_SIGN_KEY_PASSWORD")
ext."signing.secretKeyRingFile" = System.getenv("OSSRH_SIGN_KEY_PATH")
signing {
    sign publishing.publications
}

task publish {
    doLast {
        project.logger.info("Publishing sub projects")
    }
}
subprojects.each { publish.dependsOn it.tasks.publish }
